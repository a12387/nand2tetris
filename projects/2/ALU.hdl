// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    Mux16(a = x, b = false, sel = zx, out = xz);
    Not16(in = xz, out = nxz);
    Mux16(a = xz, b = nxz, sel = nx, out = xn);
    Mux16(a = y, b = false, sel = zy, out = yz);
    Not16(in = yz, out = nyz);
    Mux16(a = yz, b = nyz, sel = ny, out = yn);
    Add16(a = xn, b = yn, out = o1);
    And16(a = xn, b = yn, out = o2);
    Mux16(a = o2, b = o1, sel = f, out = o);
    Not16(in = o, out = noto);
    Mux16(a = o, b = noto, sel = no, out[0..7] = tout1, out[8..14] = tout2, out[15] = tout3);
    Or8Way(in = tout1, out = or1);
    Or8Way(in[0..6] = tout2, in[7] = tout3, out = or2);
    Or(a = or1, b = or2, out = nzr);
    Not(in = nzr, out = zr);
    And(a = tout3, b = true, out = ng);
    And16(a[0..7] = tout1, a[8..14] = tout2, a[15] = tout3, b = true, out = out);
}